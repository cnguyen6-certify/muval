plugins {
    id 'java'
    id 'groovy'
    id 'application'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

group = 'com.github.rahulsom' // This could become 'gov.nist'
version = '1.0-SNAPSHOT'

mainClassName = "gov.nist.mu.validation.Validator"

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            srcDir 'nist/generated/src/java'
        }
        resources {
            srcDir 'src/main/resources'
            srcDir 'build/generated/resources'
        }
    }
}

repositories {
    jcenter()
}

Map<String, String> xmlbeans(String subproject = null) {
    [group: 'org.apache.xmlbeans', name: subproject ? "xmlbeans-$subproject" : 'xmlbeans', version: '2.4.0']
}

Map<String, String> saxon(String subproject = null) {
    [group: 'net.sourceforge.saxon', name: 'saxon', version: '9.1.0.8', classifier: subproject]
}

configurations {
    buildsrc
}

dependencies {

    buildsrc 'org.codehaus.groovy:groovy-all:2.4.4'
    buildsrc 'com.ning:async-http-client:1.9.33'
    buildsrc 'io.netty:netty:3.10.5.Final'
    buildsrc 'commons-lang:commons-lang:2.6'

    compile 'commons-cli:commons-cli:1.0'
    compile 'javax.xml.bind:jsr173_api:1.0'
    compile 'org.dspace.xmlui.xml:xml-commons-resolver:1.1'

    compile saxon()
    compile saxon('dom')
    compile saxon('dom4j')
    compile saxon('jdom')
    compile saxon('s9api')
    compile saxon('sql')
    compile saxon('xom')
    compile saxon('xqj')

    compile xmlbeans()
    compile(xmlbeans('xpath')) {
        exclude group: 'net.sf.saxon'
    }
    compile xmlbeans('qname')
    compile xmlbeans('xmlpublic')

    testCompile 'org.codehaus.groovy:groovy-all:2.4.4'
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
}

task downloadResources(type: JavaExec) {
    description 'Download Schemas and Schematron files from NIST'
    
    classpath file("${rootDir}/buildSrc/build/classes/main")
    classpath configurations.buildsrc

    main = 'buildscript.DownloadHelper'
    args = ['docTypes.xml', 'nist']
}

task "createStylesheet"(type: Zip) {
    description "Package stylesheet"
    from "schematron"
    include "*.xsl"
    destinationDir file('build/generated/resources')
    archiveName "stylesheet"
}

task "createCachefile"(type: Zip) {
    description "Package Cachefile"
    from "nist/generated/prebundle"
    destinationDir file('build/generated/resources')
    archiveName "cachefile"
}

compileJava.dependsOn 'createStylesheet', 'createCachefile'

publishing {
    publications {
        shadow(MavenPublication) {
            from components.java
            artifact shadowJar
        }
    }
}